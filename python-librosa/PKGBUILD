# Maintainer: Jingbei Li <i@jingbei.li>
# Contributer: Jose Riha <jose1711 gmail com>
pkgname=python-librosa
_pkgname=librosa
pkgver=0.6.3
pkgrel=1
pkgdesc="Python library for music and audio analysis"
arch=('any')
_github="librosa/librosa"
url="http://librosa.github.io/librosa"
license=('ISC')
makedepends=('python-setuptools')
source=("https://github.com/librosa/librosa/archive/${pkgver}.tar.gz")
md5sums=('f77e6d1db202934e4f53699793222009')
depends=(
  'python-audioread'
  'python-numpy'
  'python-scipy'
  'python-scikit-learn'
  'python-joblib'
  'python-decorator'
  'python-six'
  'python-resampy'
  'python-matplotlib'
)

build() {
  msg "Building Python 3"
  cd "$srcdir/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
