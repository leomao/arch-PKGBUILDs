# Maintainer: Leo Mao

##### Configuration Options
# Specify GPU compute capability Fermi (2.x) or Kepler (3.x) or Maxwell (5.x)
# Can be one of these:
# Maxwell
# sm52: GTX 980
# sm53: Tegra TX1 / Tegra X1
# sm60: Pascal P100
# sm61: GTX 1080, 1070, 1060, Tesla P40
##### End

pkgname=faiss-mkl
_pkgname=faiss
pkgver=1.6.0
pkgrel=1
provides=('faiss')
conflicts=('faiss')
pkgdesc="A library for efficient similarity search and clustering of dense vectors. (with CUDA and MKL)"
arch=("x86_64")
_github="facebookresearch/faiss"
url="https://github.com/facebookresearch/faiss"
license=(BSD)
depends=("cuda" "python" "python-numpy" "intel-mkl")
makedepends=("swig")
sha1sums=('105f5d8b9d4ab60beb4db8e3349a71cf2f881422')
source=("$pkgname-$pkgver.tar.gz::${url}/archive/v${pkgver}.tar.gz")

#prepare() {
  #cd "${srcdir}/$_pkgname-$pkgver"
#}

build() {
  cd "${srcdir}/$_pkgname-$pkgver"
  export LDFLAGS="-L/opt/intel/mkl/lib/intel64"
  ./configure --with-cuda=/opt/cuda --prefix=/usr
  make all
  make -C python
}

package() {
  cd "${srcdir}/$_pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${_pkgname}/LICENSE"
  make DESTDIR="$pkgdir" install

  cd python
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
