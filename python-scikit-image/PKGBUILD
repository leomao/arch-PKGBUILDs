pkgname=python-scikit-image
_pkgname=scikit-image
pkgver=0.15.0
pkgrel=1
pkgdesc="Image Processing SciKit (Toolbox for SciPy)"
arch=('x86_64')
_github="scikit-image/scikit-image"
url="http://scikit-image.org"
license=('BSD')
depends=('python' 'python-networkx' 'python-matplotlib' 'python-pywavelets' 'python-pillow' 'python-scipy' 'python-six')
makedepends=('python-setuptools' 'cython')
optdepends=('python-imageio')
source=("https://github.com/scikit-image/scikit-image/archive/v$pkgver.tar.gz")
md5sums=('426a92b52ddc05f7e62b4fbd4ff4d440')


build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
