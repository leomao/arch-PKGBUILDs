# $Id: PKGBUILD 164237 2012-07-28 03:14:33Z stephane $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Adapted to mkl by Simone Riva
pkgname="python-numpy-mkl-gcc"
pkgver=1.16.2
pkgrel=1
pkgdesc="Scientific tools for Python compiled with intel mkl using gcc"
arch=('x86_64')
license=('custom')
options=('staticlibs')
_github="numpy/numpy"
url="http://numpy.scipy.org/"
makedepends=('python-setuptools' 'python-nose' 'cython' 'glibc' 'gcc-fortran')
depends=('intel-mkl' 'python')
provides=("python3-numpy=${pkgver}" "python-numpy=${pkgver}" "python-numpy-mkl=${pkgver}")
replaces=('python3-numpy')
conflicts=('python3-numpy' 'python-numpy')

source=(
  https://github.com/numpy/numpy/archive/v${pkgver}.tar.gz
  'site.cfg'
)

sha256sums=('88835849921a970dae7705b0ea4a144d6b22f5e28148e17f25723c492a74a3c0'
            'ae2113417043800eb7fe3d2282ebb5bed051d8562e137563c4a0f785b7e25dc4')

build() {
  export Atlas=None

  export LDFLAGS="$LDFLAGS -shared -ldl -lm"
  export CFLAGS="-fopenmp -m64 -mtune=native -O3 -Wl,--no-as-needed"
  export CXXFLAGS="-fopenmp -m64 -mtune=native -O3 -Wl,--no-as-needed"
  export FFLAGS="-fopenmp -m64 -mtune=native -O3"

  echo "Building Python3"
  cd "${srcdir}"
  cp ${srcdir}/site.cfg "${srcdir}/numpy-${pkgver}"
  cd "${srcdir}/numpy-${pkgver}"

  python setup.py config build_clib build_ext

}

package() {

  _pylib_path=$(python -c "import distutils.sysconfig; print(distutils.sysconfig.get_python_lib())")
  _pyinc_path=$(python -c "import distutils.sysconfig; print(distutils.sysconfig.get_python_inc())")


  cd "${srcdir}/numpy-${pkgver}"
  python setup.py config_fc install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-numpy/"

  install -m755 -d "${pkgdir}${_pyinc_path}"
  ln -sf ${_pylib_path}/numpy/core/include/numpy "${pkgdir}${_pyinc_path}/numpy"
}
