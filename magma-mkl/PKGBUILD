# Maintainer: Leo Mao
# Previous Maintainer: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

##### Configuration Options
# Specify GPU compute capability Fermi (2.x) or Kepler (3.x) or Maxwell (5.x)
# Can be one of these:
# Maxwell
# sm52: GTX 980
# sm53: Tegra TX1 / Tegra X1
# sm60: Pascal P100
# sm61: GTX 1080, 1070, 1060, Tesla P40
# YOU MAY NEED TO CHANGE THIS VALUE
_GPU_TARGET="sm_61 sm_75"
##### End

pkgname=magma-mkl
pkgver=2.5.2
pkgrel=1
pkgdesc="Provides a dense linear algebra library similar to LAPACK but for heterogeneous/hybrid architectures, starting with current 'Multicore+GPU' systems. (with CUDA and MKL)"
arch=("x86_64")
url="http://icl.cs.utk.edu/magma/"
license=(custom)
depends=("cuda>=6.5.0" "gsl" "python" "intel-mkl")
makedepends=("gcc-fortran")
options=('staticlibs')
sha1sums=('f69713f031a569b8b0865585c62346472c67033e')
source=("http://icl.cs.utk.edu/projectsfiles/magma/downloads/magma-${pkgver}.tar.gz")
provides=('magma')
conflicts=('magma')

prepare() {
  cd "${srcdir}/magma-${pkgver}"
}

build() {
  cd "${srcdir}/magma-${pkgver}"

  # Modify Makefile
  cp make.inc-examples/make.inc.mkl-gcc make.inc

  export GPU_TARGET=${_GPU_TARGET}
  export CUDADIR=/opt/cuda
  source /opt/intel/mkl/bin/mklvars.sh intel64
  ## compiled with gcc, no need for the following
  ##source /opt/intel/bin/compilervars.sh intel64
  ## For MKL 2018, this is a symlink to /opt/intel/compilers_and_libraries_2018.0.128/linux/bin/compilervars.sh

  make clean
  make lib
  make sparse
  make test
}

package() {
  cd "${srcdir}/magma-${pkgver}"

  export GPU_TARGET=${_GPU_TARGET}
  export CUDADIR=/opt/cuda
  source /opt/intel/mkl/bin/mklvars.sh intel64

  make DESTDIR="${pkgdir}" prefix="/opt/magma" install
  #rm "${pkgdir}/opt/magma/include/*.mod"

  #mkdir -p ${pkgdir}/opt/magma/example
  #cp -ru ${srcdir}/magma-${pkgver}/example/* ${pkgdir}/opt/magma/example/

  #mkdir -p ${pkgdir}/opt/magma/testing
  #cp -ru ${srcdir}/magma-${pkgver}/testing/* ${pkgdir}/opt/magma/testing/

  # correct the prefix in .pc file
  mkdir -p ${pkgdir}/usr/lib
  mv ${pkgdir}/opt/magma/lib/pkgconfig ${pkgdir}/usr/lib/

  mkdir -p ${pkgdir}/usr/share/licenses/magma
  cp ${srcdir}/magma-${pkgver}/COPYRIGHT ${pkgdir}/usr/share/licenses/magma/LICENSE

  # Add libmagma to LD_LIBRARY_PATH
  mkdir -p "${pkgdir}/etc/ld.so.conf.d/"
  echo "/opt/magma/lib" > "${pkgdir}/etc/ld.so.conf.d/magma.conf"
}
