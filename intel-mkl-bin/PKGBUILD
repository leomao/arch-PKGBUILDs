# Maintainer: Leo Mao
# Previous Maintainer: Ignat Harczuk: ignathe -at- gmail com
# Previous Maintainer: Mark Wells: mwellsa -at- gmail com
# Contributor: simone riva: siomone.rva -at- gmail com
# Intel Performance Libraries
##########################################################################
# intel-mkl:                    Intel Math Kernel Library (Intel® MKL)
# intel-mpi:                    Intel Message passing interface (MPI)
# intel-tbb:                    Intel Threading Building Blocks (TBB)
###########################################################################

# Copyright (C) 2017       Leo Mao
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

pkgbase="intel-mkl-bin"
pkgname=("intel-mkl-bin" "intel-openmp-bin")
_packager="Leo Mao"

########################################
#OPTIONS begin
# set to true if you want to remove documentations and examples form the packages.
#_remove_docs=TRUE
_remove_docs=true

_without_pgi_support=true
_without_openmpi_support=true

# set to true if you want to remove the static objects from the libs.
_remove_static_objects_mkl=true
#_remove_static_objects_mkl=false

_remove_static_objects_openmp=true
#_remove_static_objects_mkl=false
########################################

_year='2019'
_v_a='1'
_v_b='144'

_dir_nr='14895'

# year, version a and version b found in /opt/intel/compilers_and_libraries_YEAR_A_B

pkgrel=1
pkgver=${_year}.${_v_a}.${_v_b}
options=(strip libtool staticlibs)

pkgdesc="Intel Math Kernel Library (Intel® MKL)"
depends=('bash' 'gcc')
url="http://software.intel.com/en-us/articles/non-commercial-software-download/"
arch=('x86_64')
license=('custom')
makedepends=('libarchive' 'sed' 'gzip')

_intel_mkl_dir=l_mkl_${pkgver}

source=(
  "http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/${_dir_nr}/${_intel_mkl_dir}.tgz"
  'EULA.txt'
  'intel-mkl.conf'
  'intel-mkl.sh'
  'intel-mkl.install'
  'intel-mkl-th.conf'
  'intel-openmp.conf'
)

sha256sums=('5205a460a9c685f7a442868367389b2d0c25e1455346bc6a37c5b8ff90a20fbb'
            '228ac25e147adb9b872e1a562e522d2fd48809ccae89b765112009896a6d55a5'
            'a856326362e9b80c19dc237cbf66bf3d96a69bd7ad1baff99ec9849f8208348c'
            'fa983e00cc9005d88317af331f132e1cabcb5fbbf8d13971ab9f8ccd9bb9ee9f'
            '181000db2ac371967fefdf2b33f76dbed7f8d625d409ce19ab8e324f9d90a69f'
            '429219830e289131eecd3ece5ac9308160cca4c311f44954ad6d19839c6ebae4'
            '1511e2a1242950398129b8c149fecc464dc2db876ab6a0c8fd694f2ff524f01c')


# support 64bit only
_i_arch='intel64'
_i_arch2='x86_64'

_not_arch='ia32'
_not_arch2='i486'

package_intel-mkl-bin() {
  provides+=('intel-mkl')
  conflicts+=('intel-mkl')
  install=intel-mkl.install

  rpm_dir=${srcdir}/$_intel_mkl_dir/rpm

  cd $pkgdir
  find_condition="! -name '*installer*' ! -name '*doc*' ! -name '*psxe*' ! -name '*32bit*'"

  if [[ $_without_pgi_support ]]; then
    find_condition="$find_condition ! -name '*pgi*'"
  fi
  if [[ $_without_openmpi_support ]]; then
    find_condition="$find_condition ! -name '*cluster*'"
  fi

  eval "find $rpm_dir \
    -name 'intel-mkl-*' \
    $find_condition \
    -exec bsdtar -xf {} \;
  "

  inteldir=opt/intel/compilers_and_libraries_${pkgver}
  installdir=${inteldir}/linux
  ln -srfT ${pkgdir}/${installdir}/mkl ${pkgdir}/opt/intel/mkl

  find ${pkgdir}/${installdir} -type f -iname '*.csh' -delete
  # set prefix in all *.sh
  sed -i "s+<INSTALLDIR>+/${installdir}+g" \
    ${pkgdir}/${installdir}/mkl/bin/mklvars.sh \
    ${pkgdir}/${installdir}/mkl/bin/pkgconfig/*.pc

  echo -e " # intel-mkl: remove locale"
  rm -rf ${pkgdir}/${installdir}/mkl/lib/${_i_arch}/locale

  if ${_remove_docs} ; then
    echo -e " # intel-mkl: remove documentation"
    rm -rf ${pkgdir}/${installdir}/mkl/examples
    rm -rf ${pkgdir}/${installdir}/mkl/benchmarks
  fi

  if ${_remove_static_objects_mkl} ; then
    echo -e " # intel-mkl: remove static objects"
    rm -f ${pkgdir}/${installdir}/mkl/lib/${_i_arch}/libmkl_*.a
    rm -f ${pkgdir}/${installdir}/mkl/bin/pkgconfig/mkl-static-*.pc
  fi

  mkdir -p ${pkgdir}/usr/lib
  mv ${pkgdir}/${installdir}/mkl/bin/pkgconfig ${pkgdir}/usr/lib/

  install -Dm644 ${pkgdir}/${inteldir}/licensing/mkl/en/license.txt "${pkgdir}/usr/share/licenses/intel-mkl/LICENSE.txt"
  rm -r ${pkgdir}/${inteldir}/licensing

  install -Dm644 ${srcdir}/intel-mkl.conf "${pkgdir}/etc/ld.so.conf.d/intel-mkl.conf"
  sed -i "s/<arch>/${_i_arch}/" ${pkgdir}/etc/ld.so.conf.d/intel-mkl.conf
  install -Dm644 ${srcdir}/intel-mkl-th.conf "${pkgdir}/etc/intel-mkl-th.conf"
  install -Dm755 ${srcdir}/intel-mkl.sh "${pkgdir}/etc/profile.d/intel-mkl.sh"
}

package_intel-openmp-bin() {
  provides+=('intel-openmp')
  conflicts+=('intel-openmp')
  rpm_dir=${srcdir}/$_intel_mkl_dir/rpm

  cd $pkgdir
  find $rpm_dir \
    -name 'intel-openmp-*' \
    ! -name '*32bit*' \
    -exec bsdtar -xf {} \;

  inteldir=opt/intel/compilers_and_libraries_${pkgver}
  installdir=${inteldir}/linux
  ln -srfT ${pkgdir}/${installdir}/compiler/lib ${pkgdir}/opt/intel/lib

  echo -e " # intel-openmp: remove locale"
  rm -rf ${pkgdir}/${installdir}/compiler/lib/${_i_arch}/locale

  echo -e " # intel-openmp: remove shared library with debug symbol"
  rm -f ${pkgdir}/${installdir}/compiler/lib/${_i_arch}/*.dbg

  if ${_remove_static_objects_openmp} ; then
    echo -e " # intel-openmp: remove static objects"
    rm -f ${pkgdir}/${installdir}/compiler/lib/${_i_arch}/*.a
  fi

  install -Dm644 ${srcdir}/intel-openmp.conf "${pkgdir}/etc/ld.so.conf.d/intel-openmp.conf"
  sed -i "s/<arch>/${_i_arch}/" ${pkgdir}/etc/ld.so.conf.d/intel-openmp.conf
}
